apply plugin: 'java'
apply from: "./libraries.gradle"

project.ext {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

allprojects {
    repositories {
        mavenCentral()
    }

    // YOU SHALL NOT PASS!
    configurations {
        all*.exclude module: 'commons-logging'
    }
}


subprojects { subProject ->
    apply plugin: 'java'
    apply plugin: 'idea'

    targetCompatibility = rootProject.sourceCompatibility
    sourceCompatibility = rootProject.targetCompatibility

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    // common dependencies for all projects
    dependencies {
        // logging
        compile libraries.logback

        // testing
        testCompile libraries.festAssertions
        testCompile libraries.mockito
        testCompile libraries.testNg
    }

    sourceSets {
        main {
            resources {
                srcDirs 'src/main/resources'
                srcDirs 'src/main/properties'
            }
        }

        test {
            java {
                srcDirs 'src/test/'
            }
        }
    }

    task "create-dirs"  {
        description =  "creates basic directory structure for given project"

        sourceSets*.java.srcDirs*.each {
            it.mkdirs()
        }

        sourceSets*.resources.srcDirs*.each {
            it.mkdirs()
        }
    }
}

configure(rootProject) {

    task wrapper(type: Wrapper) {
        description = "Generates gradlew[.bat] scripts"
        gradleVersion = "1.11"


        doLast() {
            def gradleOpts = ""
            def gradleBatOpts = "$gradleOpts -XX:MaxHeapSize=256m"
            File wrapperFile = file("gradlew")
            wrapperFile.text = wrapperFile.text.replace("DEFAULT_JVM_OPTS=",
                "GRADLE_OPTS=\"$gradleOpts \$GRADLE_OPTS\"\nDEFAULT_JVM_OPTS=")
            File wrapperBatFile = file("gradlew.bat")
            wrapperBatFile.text = wrapperBatFile.text.replace("set DEFAULT_JVM_OPTS=",
                "set GRADLE_OPTS=$gradleBatOpts %GRADLE_OPTS%\nset DEFAULT_JVM_OPTS=")
        }
    }   
}

test {
    useTestNG()
}
